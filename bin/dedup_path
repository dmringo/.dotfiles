#!/usr/bin/awk -f

# This script expects a PATH-like variable (PATH, MANPATH, etc.) to be passed on
# STDIN, and prints a valid PATH with trailing duplicates removed.  It could
# probably be modified to read ENVIRON["SOMEPATH"], but I like the idiomatic
# record-by-record approach.


BEGIN { RS=":" }

{
  # any time we see a new element, save it in the `path` and
  # record it as a key in `uniqs`
  if (!($0 in uniqs)) { # checks existence of $0 as *key* in array
    uniqs[$0]=1
    path[count++] = $0
  }
}

# after were done reading the input, just print the filtered PATH in order
END {
  # we want to print a valid PATH, so use colon as the record separator
  ORS=":"
  for(i=0; i < count; i++)
  {
    # dont want a trailing colon, so reset ORS on the last element
    if(i==count-1) ORS="";
    print path[i]
  }
}
